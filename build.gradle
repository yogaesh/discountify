buildscript {
	ext {
		springBootVersion = '1.4.0.M1'
	}
	repositories {
		mavenCentral()
		maven { url "https://repo.spring.io/snapshot" }
		maven { url "https://repo.spring.io/milestone" }
	}
	dependencies {
		classpath("org.springframework.boot:spring-boot-gradle-plugin:${springBootVersion}") 
	}
}

apply plugin: 'java'
apply plugin: 'eclipse'
apply plugin: 'spring-boot'
apply plugin: 'jacoco'

jacocoTestReport {
    group = "Reporting"
    reports {
        xml.enabled true
        csv.enabled false
        html.destination "${buildDir}/reports/coverage"
    }
    
     afterEvaluate {
        classDirectories = files(classDirectories.files.collect {
            fileTree(dir: it,
                    exclude: ['com/discountify/pojo/**',
                    		  'com/discountify/exception/**',
                    		  'com/discountify/item/categories/**',
                    		  'com/discountify/controllers/**',
                    		  'com/discountify/DiscountifyApplication*'])
        })
    }
}

jar {
	baseName = 'discountify'
	version = '0.0.1-SNAPSHOT'
}
sourceCompatibility = 1.8
targetCompatibility = 1.8

repositories {
	mavenCentral()
	maven { url "https://repo.spring.io/snapshot" }
	maven { url "https://repo.spring.io/milestone" }
}


dependencies {
	compile('org.projectlombok:lombok')
	compile('com.google.code.gson:gson:2.6.2')
	compile('com.google.guava:guava:19.0')
	compile('com.h2database:h2:1.4.191')
	compile('org.springframework.boot:spring-boot-starter-data-jpa')
	compile('org.springframework.boot:spring-boot-starter-web')
	testCompile('org.springframework.boot:spring-boot-starter-test') 
	testCompile('com.jayway.restassured:rest-assured:2.9.0')
}

eclipse {
       classpath {
                containers.remove('org.eclipse.jdt.launching.JRE_CONTAINER')
                containers 'org.eclipse.jdt.launching.JRE_CONTAINER/org.eclipse.jdt.internal.debug.ui.launcher.StandardVMType/JavaSE-1.8'
       }
}

task wrapper(type: Wrapper) {
       gradleVersion = '2.9'
}
